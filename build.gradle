plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '6.23.0'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'aws-member-manager'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'com.h2database:h2'

    runtimeOnly 'com.mysql:mysql-connector-j'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store:4.0.0-RC1'
}

springBoot {
	buildInfo()
}

tasks.named('test') {
    useJUnitPlatform()
}

// formatter 설정
spotless {
    java {
        palantirJavaFormat()
    }
}

// mysql-schema.sql 생성
// https://dev.to/jakub_zalas/how-to-run-spring-boot-with-a-dev-profile-and-gradle-2jm
tasks.register('generateSchema') {
    dependsOn 'generateSchemaPrep'
    group = 'build'
    description = 'Generate mysql-schema.sql'

    // hibernate가 windows에서는 
    // \n 대신에 \r\n을 써서 새 줄을 만듭니다...
    // 뭔가 설정을 더 건드리면 될거 같지만... 이게 더 간편해서요
    doFirst {
        def schemaFile = file('mysql-schema.sql')
        if (schemaFile.exists()) {
            schemaFile.text = schemaFile.text.replaceAll('\r\n', '\n')
        }
    }
}

tasks.register('generateSchemaPrep') {
    doFirst {
        delete file('mysql-schema.sql')

        tasks.bootRun.configure {
            systemProperty('spring.config.additional-location', 'file:./application-mysql-schema.yml')
        }
    }

    finalizedBy('bootRun')
}

// encoding 설정
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}
tasks.withType(Test).configureEach {
    systemProperty "file.encoding", "UTF-8"
}

